# Paths 
PathToRef = '../data/input/reference/'
PathToRaw = '../data/input/samples/'
PathToTemp = '../data/temp/'
PathToOutput = '../data/output/'
PathToEnv = 'environment/env.yml'


rule gunzip:
    input:
        R1 = ancient(PathToRaw + "{sample}_R1.fastq.gz"),
        R2 = ancient(PathToRaw + "{sample}_R2.fastq.gz")
    output:
        R1 = PathToRaw + "{sample}_R1.fastq",
        R2 = PathToRaw + "{sample}_R2.fastq"
    conda:
        PathToEnv
    shell: 
        """
        parallel gunzip -k ::: {input}
        """


rule index:
    input:
        reference = ancient(PathToRef + "ref_mitochondrial.fasta")
    output:
        sample_fasta = PathToTemp + "ref_mitochondrial.fasta",
        bwa_index = PathToTemp + "ref_mitochondrial.fasta.bwt",
        fai_index = PathToTemp + "ref_mitochondrial.fasta.fai",
        dictionary = PathToTemp + "ref_mitochondrial.dict"
    conda:
        PathToEnv
    shell:
        """
        cp {input.reference} {output.sample_fasta}
        bwa index {output.sample_fasta}
        samtools faidx {output.sample_fasta}
        gatk CreateSequenceDictionary -R {output.sample_fasta} 
        """


rule mapping:   # 1100s 4 threads
    input:
        paz = ancient(PathToTemp + "{sample}/"),
        reference = ancient(PathToTemp + "ref_mitochondrial.fasta"),
        R1 = ancient(PathToRaw + "{sample}_R1.fastq"),
        R2 = ancient(PathToRaw + "{sample}_R2.fastq")
    params:
        thread = config['thread']
    output:
        PathToTemp + "{sample}/{sample}.bam"
    conda:
        PathToEnv
    shell:
        """
        bwa mem -t {params.thread} {input.reference} {input.R1} {input.R2} | samtools view -@ 4 -bh | samtools sort -@ 4 -o {output}
        """


rule bam_index: # 58s 4 threads
    input:
        bam = ancient(PathToTemp + "{sample}/{sample}.bam")
    output:
        bam_ind = PathToTemp + "{sample}/{sample}.bam.bai"
    conda:
        PathToEnv
    shell:
        "samtools index {input.bam}"


rule add_groups:
    input:
        bam = ancient(PathToTemp + "{sample}/{sample}.bam")
    output:
        bam = PathToTemp + "{sample}/{sample}.groups.bam"
    params:
        name = "{sample}"
    conda:
        PathToEnv
    shell:
        "picard AddOrReplaceReadGroups -I {input.bam} -O {output.bam} -RGLB lib.HSmt -RGPL illumina -RGPU NO_GROUP -RGSM {params.name}"


rule mark:
    input:
        bam_ind = PathToTemp + "{sample}/{sample}.bam.bai",
        bam = PathToTemp + "{sample}/{sample}.groups.bam"
    output:
        bam_marked = PathToTemp + "{sample}/{sample}.marked.bam"
    conda:
        PathToEnv
    shell:
        "gatk MarkDuplicatesSpark -I {input.bam} -O {output.bam_marked}" 


rule filter:
    input:
        bam = PathToTemp + "{sample}/{sample}.marked.bam"
    output:
        bam_filtered = PathToTemp + "{sample}/{sample}.filtered.bam"
    conda:
        PathToEnv
    shell:
        "samtools view -h -@ 4 -f  0x2 -F 0x4 -F 0x8 -F 0x100 {input.bam} | samtools view -bh -@ 4 -o {output.bam_filtered}"


rule sort:
    input:
        PathToTemp + "{sample}/{sample}.filtered.bam"
    output:
        PathToTemp + "{sample}/{sample}.sorted.bam"
    conda:
        PathToEnv
    shell:
        "picard FixMateInformation -I {input} -O {output} -SO coordinate"


rule build_index:
    input:
        PathToTemp + "{sample}/{sample}.sorted.bam"
    output:
        PathToTemp + "{sample}/{sample}.sorted.bai"
    conda:
        PathToEnv
    shell:
        "picard BuildBamIndex -I {input} -O {output}"

if config['consensus']:
    rule consensus:   # 457s
        input:
            PathToTemp + "{sample}/{sample}.sorted.bai",
            reference = PathToTemp + "ref_mitochondrial.fasta",
            target_bam = PathToTemp + "{sample}/{sample}.sorted.bam"
        output:
            PathToTemp + "{sample}/{sample}.fasta"
        conda:
            PathToEnv
        shell:
            "samtools consensus -f fasta -a {input.target_bam} -o {output}"
    

    rule find_haplogroup_consensus:
        input:
            PathToTemp + "{sample}/{sample}.fasta"
        output:
            PathToOutput + "{sample}.txt"
        conda:
            PathToEnv
        shell:
            "haplogrep classify --in {input} --out {output} --format fasta"
else:
    rule variant_calling:   # 457s
        input:
            PathToTemp + "{sample}/{sample}.sorted.bai",
            reference = PathToTemp + "ref_mitochondrial.fasta",
            target_bam = PathToTemp + "{sample}/{sample}.sorted.bam"
        output:
            PathToOutput + "{sample}.vcf"
        params:
            thread = config['thread']
        conda:
            PathToEnv
        shell:
            "gatk Mutect2 --native-pair-hmm-threads {params.thread} -R {input.reference} -I {input.target_bam} -O {output}"


    rule find_haplogroup_vcf:
        input:
            PathToOutput + "{sample}.vcf"
        output:
            PathToOutput + "{sample}.txt"
        conda:
            PathToEnv
        shell:
            """
            haplogrep classify --in {input} --out {output} --format vcf
            rm ../data/output/{wildcards.sample}.vcf.idx
            rm ../data/output/{wildcards.sample}.vcf.stats
            rm ../data/output/{wildcards.sample}.vcf
            """
